grammar de.thm.icampus.ejsl.EJSL with org.eclipse.xtext.common.Terminals
//import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
generate eJSL "http://www.thm.de/icampus/ejsl/EJSL"

EJSLModel returns EJSLModel:
	{EJSLModel}
	'eJSLModel'	name=STRING
	'{'
		('datatypes' '{' datatypes+=Datatype ( "," datatypes+=Datatype)* '}')?
		('globalparameters' '{' (globalparameters+=Parameter)* '}')?
		('entities' '{' (entities+=Entity)* '}')?
		('datapackages' '{' (datapackages+=Datapackage)* '}')?
		('pages' '{' (pages+=Page)* '}')?
		('extensions' '{' (extensions+=Extension)* '}')?
	'}';

// data model
Type:
	DatatypeReference | SimpleDatatypes
;

DatatypeReference:
	type=[Datatype|STRING]
;

SimpleDatatypes:
	type=SimpleDatatypeKinds
;

Datatype returns Datatype:
	{Datatype}
	'Datatype' name=STRING;
	
Parameter returns Parameter:
	{Parameter}
	'Parameter' name=ID 
	'{'
		//'type' '=' ((dtype=[Datatype|QualifiedName]) | ('jvmtype' jvmtype=[jvmTypes::JvmType|QualifiedName]))
		'type' '=' ((dtype=Type))
		('defaultvalue' '=' defaultvalue=STRING)?
		('label' '=' label=STRING)?
		('size' '=' size=INT)?
		('description' '=' descripton=STRING)?
	'}'
;

Datapackage returns Datapackage:
	{Datapackage}
	'Datapackage' name=ID
	'{'
		('datapackages' '{' (datapackages+=Datapackage)* '}')?
		('entities' '{' (entities+=Entity)* '}')?
		('datatypes' '{' (datatypes+=Datatype)* '}')?
	'}'
;

Entity returns Entity:
	{Entity}
	'Entity' name=ID ('extends' supertype=[Entity|STRING])?
	'{'
		('attributes' '{' (attributes+=Attribute)* '}')?
		('references' '{' (references+=Reference)* '}')?
	'}'
;

Attribute returns Attribute:
	{Attribute}
	//'Attribute' name=ID ':' ((dtype=[Datatype|QualifiedName]) | ('jvmtype' jvmtype=[jvmTypes::JvmType|QualifiedName]))
	'Attribute' name=ID ':' (type=Type)
;

Reference returns Reference:
	{Reference}
	'Reference' name=ID 
	'{'
		'*Entity' entity=[Entity|STRING]
		'lower' lower=NUMBER
		'upper' upper=NUMBER 
	'}'
;

// page model

Page:
	StaticPage | DynamicPage
;

StaticPage returns StaticPage:
	{StaticPage}
	'StaticPage' name=ID 
	'{'
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('links' '{' (links+=Link)* '}')?
		'HTMLBody' '{' HTMLBody = STRING '}'
	'}'
;

DynamicPage:
	IndexPage | DetailsPage
;

IndexPage returns IndexPage:
	{IndexPage}
	'IndexPage' name=ID 
	'{'
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('links' '{' (links+=Link)* '}')?
	'}'
;

DetailsPage returns DetailsPage:
	{DetailsPage}
	'DetailsPage' name=ID 
	'{'
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('links' '{' (links+=Link)* '}')?
	'}'
;

Link:
	ExternalLink | InternalLink 
;

ExternalLink returns ExternalLink:
	{ExternalLink}
	'ExternalLink' 
	'{'
		'target' '=' target=STRING
		('label' '=' label=STRING)?
	'}'
;

InternalLink returns InternalLink:
	{InternalLink}
	('InternalLink' name=ID 
	'{'
		'target' '=' '*'target=[Page|QualifiedName]
	'}') | ContextLink
;

ContextLink returns ContextLink:
	{ContextLink}
	'InternalcontextLink' name=ID 
	'{'
		'target' '=' '*'target=[Page|QualifiedName]
		'linkparameters' '{' (linkparameters+=LinkParameter)* '}'
	'}'
;

LinkParameter returns LinkParameter:
	{LinkParameter}
	'Parameter' name=ID '=' ('*Attribute 'attvalue=[Attribute|STRING] | value=STRING)
;

// extension model

Extension:
	ExtensionPackage | Component | Module | Plugin | Library | Template	
;

ExtensionPackage returns ExtensionPackage:
	{ExtensionPackage}
	'Extension package' name=ID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		'extensions' '{' (extensions+=Extension)+ '}'
	'}'
;

Component returns Component:
	{Component}
	'Component' name=ID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		'sections' '{' (sections+=Section)+ '}'
	'}'
;

Section:
	Backend | Frontend
;

Backend returns BackendSection:
	{BackendSection}
	'Backend section' 
	'{'
		('*Pages' page+=[Page|QualifiedName] (',' page+=[Page|QualifiedName])*)?
	'}'
;

Frontend returns FrontendSection:
	{FrontendSection}
	'Frontend section' 
	'{'
		('*Pages' page+=[Page|QualifiedName] (',' page+=[Page|QualifiedName])*)?
	'}'
;

Module returns Module:
	{Module}
	'Module' name=ID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		('*Page' page=[Page|QualifiedName])?
	'}'	
;

Plugin returns Plugin:
	{Plugin}
	'Plugin' name=ID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		'Plugintype' '=' type=PluginKinds
		('languages' '{' (languages+=Language)* '}')?
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
	'}'	
;

Library returns Library:
	{Library}
	'Library' name=ID
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
		('classes' '{' (classes+=Class)* '}')?
		('packages' '{' (packages+=Package)* '}')?
	'}'
;

Package returns Package:
	{Package}
	'Package' name=ID
	'{'
		('packages' '{' (packages+=Package)* '}')?
		('classes' '{' (classes+=Class)* '}')?
	'}'
;

Class returns Class:
	{Class}
    'Class' name=ID ('extends' supertype=[Class|QualifiedName])?
    '{'
    	('*Class references' references+=[Class|QualifiedName] (',' references+=[Class|QualifiedName])*)?
    	('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
        ('methods' '{' (methods+=Method)* '}')?
    '}'
;

Method returns Method:
	{Method}
    'Method' name=ID
    '{'
        //'Returnvalue' returnvalue=ID ':' type=[jvmTypes::JvmType|QualifiedName]
        'Returnvalue' returnvalue=ID ':' type=Type
        ('methodparameters' '{' (methodparameters+=MethodParameter)* '}')?
    '}'
;

MethodParameter returns MethodParameter:
	{MethodParameter}
    //'MethodParameter' name=ID ':' type=[jvmTypes::JvmType|QualifiedName]
    'MethodParameter' name=ID ':' type=Type
;

Template returns Template:
	{Template}
	'Template' name=ID
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		('positions' '{' (positions+=Position)* '}')?
		('cssblocks' '{' (cssblocks+=CssBlock)* '}')?
	'}'
;

Manifestation returns Manifestation:
	{Manifestation}
	'authors' '{' (authors+=Author)+ '}'
	('creationdate' '=' creationdate=DATE)?
	('copyright' '=' copyright=STRING)?
	('license' '=' license=STRING)?
	('link' '=' link=STRING)?
	('version' '=' version=STRING)?
	('description' '=' description=STRING)?;

terminal DATE:
  ((('0'..'2') ('0'..'9') '.' ('02')) |
  (('0'..'2') ('0'..'9') '.' ((('0') (('1') | ('3'..'9'))) | ('1') ('0'..'2')) | (('1') ('0'..'2'))) |
  (('30') '.' ((('0') (('1') | ('3'..'9'))) | ('1') ('0'..'2'))) |
  (('31') '.' ((('0') (('1') | ('3') | ('5') | ('7') | ('8'))) | (('1') (('0')| ('2'))))))
  '.' ('0'..'2') ('0'..'9') ('0'..'9') ('0'..'9')
;

Author returns Author:
	{Author}
	'Author' name=STRING 
	'{'
		('authoremail' '=' authoremail=STRING)?
		('authorurl' '=' authorurl=STRING)?
	'}'
;

Language returns Language:
	{Language}
	'Language' name=LANGUAGE_CODE 
	'{'
    	('keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
    '}'
;

terminal LANGUAGE_CODE:
    'a'..'z' 'a'..'z' '-' 'A'..'Z' 'A'..'Z'
;

KeyValuePair returns KeyValuePair:
	{KeyValuePair}
	'Key' name=ID '=' value=STRING
;

Position returns Position:
	{Position}
	'Templateposition' (name=POSITION_TYPES | name=ID)
	'{'
		('positionparameters' '{' (positionparameters+=PositionParameter)* '}')?
	'}'
;

terminal POSITION_TYPES:
	'head' | 'contents' | 'footer' | 'left' | 'right'
;

PositionParameter:
	'Position Parameter' name = ID
	'{'
		('divId' '=' divid = ID)?
		('Positiontype' '=' type = POSITION_TYPES_NAMES)?
		('CSS keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
	'}'
;

terminal POSITION_TYPES_NAMES:
	'modules' | 'component'
;

CssBlock:
	'CssBlock' selector=STRING
	'('
		('keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
	')'
;

NUMBER : ('-')?INT;
	
QualifiedName: ID ('.' ID)* ;

enum PluginKinds:
    authenticate | captcha | content | contact |
    editors | extensions | finder | quick_icons |
    search | system | user | xml_rpc
;

enum SimpleDatatypeKinds:
	string | bool | int | float | array
;