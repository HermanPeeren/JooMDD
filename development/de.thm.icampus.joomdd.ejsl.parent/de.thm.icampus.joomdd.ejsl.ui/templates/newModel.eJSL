eJSLModel "AgendaModel"
{
	eJSL part: CMS Extension { 
	datatypes {
		Datatype editor= "editor", Datatype usergroup ="usergroup" , Datatype Button ="button"
	}
	 
	entities {
		Entity Referent {
			attributes {
				Attribute name {
					type = Short_Text Not Null Unique attribute with ID
				}
				Attribute vorname {
					type = Short_Text Not Null
				}
				Attribute herkunftland {
					type = Short_Text
				}
				Attribute geburtdatum {
					type = Date
				}
			}
		}
		Entity Raum{
			attributes {
				Attribute identification {
					type = Short_Text Not Null Unique attribute with ID
				}
				Attribute gebaeude {
					type = Short_Text
				}
				Attribute adresse {
					type = Short_Text
				}
			}
		}
		Entity Conference {
			attributes {
				Attribute titel {
					type = Short_Text Not Null Unique attribute with ID
				}
				Attribute date {
					type = Time Not Null 
				}
				Attribute raum {
					type = Short_Text Not Null 
				}
				
				Attribute referent {
					type = Short_Text Not Null
				}
				
				Attribute ^description{
					type = Text
				}
			}
			references{
				Reference {
					EntityAttribute =  raum *Entity = Raum *EntityReference =  Raum.identification lower = 1 upper = 1
				}
				
				Reference {
					EntityAttribute = referent *Entity = Referent *EntityReference = Referent.name lower = 1 upper = 1
				}
				
				
			}
			
		}
		
	}
	pages {
		IndexPage Referents {
			*Entities Referent
			pageactions {
				PageAction new {
					type = NEW
					position = top
				}
			}
			table columns = Referent.name , Referent.vorname , Referent.herkunftland
			filters = Referent.name , Referent.geburtdatum
			
			links{
				InternalcontextLink DetailsReferent{
					target = Referentdetail linked attribute = Referent.name linkparameters {
						Parameter name = *Attribute "Referent.name"
						Parameter id = *Attribute ID
					}
				}
				InternalcontextLink NewReferent{
					target = Referentedit linked action = new  linkparameters{
						Parameter id = "0"
					}
				}
			}
		}
		IndexPage ReferentsAdmin {
			*Entities Referent
			pageactions {
				PageAction new
				{
					type = NEW
					position = top
				}
			}
			table columns = Referent.name , Referent.vorname , Referent.herkunftland
			filters = Referent.name , Referent.geburtdatum
			links{
				InternalcontextLink DetailsReferent{
					target = Referentedit linked attribute = Referent.name linkparameters {
						Parameter name = *Attribute "Referent.name"
						Parameter id = *Attribute ID
					}
				}
				InternalcontextLink NewReferent{
					target = Referentedit linked action = new linkparameters{
						Parameter id = "0"
					}
				}
			}
		}
		DetailsPage Referentedit {
			*Entities Referent
			editFields {
				Referent.name {
						type =  Text_Field 
						},
				Referent.vorname {
						type =  Text_Field 
						},
				Referent.geburtdatum {
						type =  Datepicker 
						},
				Referent.herkunftland {
						type =  Text_Field 
						}
			}
			localparameters {
				Parameter referentList {
					type = Button
				}
			}
			pageactions{
				PageAction referentList{
					type = INDIVIDUAL
					position = top
				}
			}
			links {
				InternalLink gotoReferentlist {
					target = Referents linked action = referentList 
				}
			}
			
		}
		DetailsPage Referentdetail {
			*Entities Referent
			pageactions {
				PageAction referentlist {
					type = INDIVIDUAL
					position = top
				}
				PageAction referentedit {
					type = EDIT
					position = top
				}
			}
			table columns = Referent.name , Referent.vorname , Referent.herkunftland , Referent.geburtdatum
			links {
				InternalLink gotoReferentlist {
					target = Referents linked action = referentlist
				}
				InternalcontextLink referentedit{
					target = Referentedit linked action = referentedit linkparameters {
						Parameter id= *Attribute ID
					}
				}
			} 
			
		}
		IndexPage Raums {
			*Entities Raum
			table columns = Raum.identification , Raum.gebaeude , Raum.adresse
			filters = Raum.gebaeude , Raum.identification
			links{
				InternalcontextLink DetailsRaum{
					target = Raumdetail linked attribute = Raum.identification linkparameters{
						Parameter raumname = *Attribute  "Raum.identification"
						Parameter id = *Attribute ID
					}
				}
			}
			
		}
		IndexPage Raumsadmin {
			*Entities Raum
			table columns = Raum.identification , Raum.gebaeude , Raum.adresse
			filters = Raum.gebaeude , Raum.identification
			links{
				InternalcontextLink DetailsRaum{
					target = Raumedit linked attribute = Raum.identification linkparameters{
						Parameter raumname = *Attribute  "Raum.identification"
						Parameter id = *Attribute ID
					}
				}
			}
			
		}
		DetailsPage Raumedit {
			*Entities Raum
			editFields {
				Raum.identification {
						type =  Text_Field 
						},
				Raum.gebaeude {
						type =  Text_Field 
						},
				Raum.adresse {
						type =  Text_Field 
						}
			}
			
			pageactions {
			PageAction raumlist {
					type = INDIVIDUAL
					position = top
				}
				}
			links {
				InternalLink gotolist {
					target = Raums linked action = raumlist
				}
			}
			
		}
		DetailsPage Raumdetail {
			*Entities Raum
			pageactions{
					PageAction raumlist {
					type = INDIVIDUAL
					position = top
				}
				PageAction raumedit {
					type = EDIT
					position = top
				}
				
			}
			table columns = Raum.identification, Raum.gebaeude, Raum.adresse 
			links {
				InternalLink gotoRaumlist {
					target = Raums linked action = raumlist
				}
				InternalcontextLink raumdedit{
					target = Raumedit linked action = raumedit  linkparameters {
						Parameter id= *Attribute ID
					}
				}
			} 
			
			
		}
		IndexPage Conferences {
			*Entities Conference
			table columns = Conference.titel , Conference.date, Conference.referent , Conference.raum
			filters = Conference.titel , Conference.date, Conference.referent , Conference.raum
			links{
				InternalcontextLink DetailsConference{
					target = Conferencedetail linked attribute = Conference.titel linkparameters{
						Parameter id = *Attribute  ID
					}
				}
				InternalcontextLink Detailsraum{
					target = Raumdetail linked attribute = Conference.raum linkparameters{
						Parameter raumname = *Attribute  "Conference.raum"
						
						
					}
				}
			  InternalcontextLink DetailsReferent{
					target = Referentdetail linked attribute = Conference.referent linkparameters{
						Parameter name = *Attribute  "Conference.referent"
					}
				}
			  
				
			}	
		}
		IndexPage Conferencesadmin {
			*Entities Conference
			table columns = Conference.titel , Conference.date, Conference.referent , Conference.raum
			filters = Conference.titel , Conference.date, Conference.referent , Conference.raum
			links{
				InternalcontextLink DetailsConference{
					target = Conferenceedit linked attribute = Conference.titel linkparameters{
						Parameter id = *Attribute  ID
					}
				}
				InternalcontextLink Detailsraum{
					target = Raumedit linked attribute = Conference.raum linkparameters{
						Parameter raunma = *Attribute  "Conference.titel"
					}
				}
			  InternalcontextLink DetailsReferent{
					target = Referentedit linked attribute = Conference.referent linkparameters{
						Parameter name = *Attribute  "Conference.referent"
					}
				}
			  
				
			}	
		}
		DetailsPage Conferenceedit {
			*Entities Conference
			editFields {
				Conference.titel {
						type =  Text_Field 
						},
				Conference.date {
						type =  Datepicker 
						},
				Conference.raum {
						type =  Text_Field 
						},
				Conference.referent {
						type =  Text_Field 
						},
				Conference.^description {
						type =  Editor 
						}
			}
			pageactions {
				PageAction conferencelist{
					type = INDIVIDUAL position = top
				}
			}
			links {
				InternalLink gotolist {
					target = Conferences linked action =conferencelist 
				}
			}
			
		}
		DetailsPage Conferencedetail {
			*Entities Conference
			
			pageactions {
				PageAction conferencelist {
					type = INDIVIDUAL position = top
					
				}
				PageAction conferenceedit{
					type = EDIT position = top
				}
			}
			table columns = Conference.titel , Conference.date , Conference.raum , Conference.referent, Conference.^description
			links {
				InternalLink gotoConferencelist {
					target = Conferences linked action =Conferenceedit.conferencelist 
				}
				InternalcontextLink conferenceedit{
					target = Conferenceedit linked action = conferenceedit linkparameters {
						Parameter id= *Attribute ID
					}
				}
			} 
			
		}
	}
	extensions {
		Component JoomMdd {
			Manifestation {
				authors {
					Author "Dieudonne Timma"
					{
						authoremail = "dieudonnetimma@yahoo.fr"
						authorurl = "http://www.goosgle.com"
					}	
				}
				copyright = "GNU 3"
				version = "1"
				description = "This is the best versions of the worlds"
				
			}languages {
					Language de-DE{
						
					}
					Language en-GB{
						
					}
				}
			sections {
				Backend section {
					*Pages {
						*Page : ReferentsAdmin
						*Page : Referentedit
						*Page : Raumsadmin
						*Page : Raumedit
						*Page :  Conferencesadmin
						*Page : Conferenceedit
					}
				}
				Frontend section {
					*Pages {
						*Page : Referents
						*Page : Referentdetail
						*Page : Referentedit
						*Page : Raums
						*Page : Raumdetail 
						*Page : Raumedit
						*Page : Conferences
						*Page : Conferencedetail
						*Page : Conferenceedit
					}
				}
			}
		}
	}
	
	}
}