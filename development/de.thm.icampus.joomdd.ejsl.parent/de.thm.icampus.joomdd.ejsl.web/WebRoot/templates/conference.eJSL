eJSLModel "Conference" {
    eJSL part: CMS Extension {
        entities {
            Entity Participant {
                attributes {
                    Attribute name {
                        type = Short_Text
                        Unique attribute with ID
                    }
                    Attribute affiliation {
                        type = Text
                    }
                    Attribute nationality {
                        type = Text
                    }
                    Attribute address {
                        type = Text
                    }
                }
            }
            Entity Talk {
                attributes {
                    Attribute title {
                        type = Short_Text
                        Unique attribute
                    }
                    Attribute ^description {
                        type = Text
                    }
                    Attribute speaker {
                        type = Short_Text
                    }
                }
                references {
                    Reference {
 						EntityAttribute = speaker
	                    ReferencedEntity = Participant
	                    ReferencedEntityAttribute = Participant.name
	                    min = 1
	                    max = 1
                    }
                } 
            }
            Entity Room {
                attributes {
                    Attribute roomname {
                        type = Short_Text
                        Unique attribute
                    }
                    Attribute ^position {
                        type = Text
                    }
                }
            }
            Entity Programme {
                attributes {
                    Attribute title {
                        type = Short_Text
                        Unique attribute
                    }
                    Attribute time {
                        type = Datetime
                    }
                    Attribute talk {
                        type = Short_Text
                    }
                    Attribute room {
                        type = Short_Text
                    }
                }
                references {
                    Reference {
                        EntityAttribute = talk
                        ReferencedEntity = Talk
                        ReferencedEntityAttribute = Talk.title
                        min = 0
                        max = 1
                    }
                    Reference {
                        EntityAttribute = room
                        ReferencedEntity = Room
                        ReferencedEntityAttribute = Room.roomname
                        min = 1
                        max = 1
                    }
                }
            }
        }
        pages {
            IndexPage Participants {
                *Entities Participant
                representation columns = Participant.name, Participant.address, Participant.affiliation
                filters = Participant.name, Participant.affiliation 
                links {
                    InternalcontextLink Details {
                        target = Participant 
                        linked attribute = Participant.name
                        linkparameters {
                            Parameter name = *Attribute  "Participant.name"
                        }
                    }
                }
            }
            DetailsPage Participant {
                *Entities Participant 
                links {
                    InternalLink Index {
                        target = Participants 
                        linked attribute = name
                    }
                }
            }
            IndexPage Talks {
                *Entities Talk
                representation columns = Talk.title, Talk.speaker
                filters = Talk.title, Talk.speaker 
                links {
                    InternalcontextLink Details {
                        target = Talk 
                        linked attribute = Talk.title
                        linkparameters {
                            Parameter title = *Attribute  "Talk.title"
                        }
                    }
                }
            }
            DetailsPage Talk {
                *Entities Talk 
                links {
                    InternalLink Index {
                        target = Talks 
                        linked attribute = title
                    }
                }
            }
            IndexPage Rooms {
                *Entities Room
                representation columns = Room.roomname, Room.^position
                filters = Room.roomname, Room.^position 
                links {
                    InternalcontextLink Details {
                        target = Room 
                        linked attribute = Room.roomname
                        linkparameters {
                            Parameter roomname = *Attribute  "Room.roomname"
                        }
                    }
                }
            }
            DetailsPage Room {
                *Entities Room 
                links {
                    InternalLink Index {
                        target = Rooms 
                        linked attribute = roomname
                    }
                }
            }
            IndexPage Programme {
                *Entities Programme
                representation columns = Programme.room, Programme.talk, Programme.time, Programme.title
                filters = Programme.room, Programme.talk, Programme.time, Programme.title 
                links {
                    InternalcontextLink Details {
                        target = Session 
                        linked attribute = Programme.title
                        linkparameters {
                            Parameter title = *Attribute  "Programme.title"
                        }
                    }
                }
            }
            DetailsPage Session {
                *Entities Programme 
                links {
                    InternalLink Index {
                        target = Programme 
                        linked attribute = Programme.title
                    }
                }
            }
        }
        extensions {
            Component MyConference {
                Manifestation {
                    authors {
                        Author "John Doe" {
                            authoremail = "john.doe@example.org"
                        }
                    }
                    copyright = "Copyright (C) 2018 All right reserved."
                    license = "GNU General Public License"
                    version = "1.0.1"
                }
                languages {
                    Language de-DE {}
                    Language system de-DE {}
                    Language en-GB {}
                    Language system en-GB {}
                }
                sections {
                    Frontend section {
                        *Pages {
                            *Page : Participants
                            *Page : Participant
                            *Page : Talks
                            *Page : Talk
                            *Page : Rooms
                            *Page : Room
                            *Page : Programme
                            *Page : Session
                        }
                    }
                    Backend section {
                        *Pages {
                            *Page : Participants
                            *Page : Participant
                            *Page : Talks
                            *Page : Talk
                            *Page : Rooms
                            *Page : Room
                            *Page : Programme
                            *Page : Session
                        }
                    }
                }
            }
        }   
    }
}