eJSLModel "Conference" {
 datatypes {
  Datatype "varchar(255) NOT NULL",
  Datatype "text NOT NULL",
  Datatype "datetime NOT NULL DEFAULT '0000-00-00 00:00:00'",
  Datatype "date",
  Datatype "textarea",
  Datatype "text",
  Datatype "editor"  
 }
 entities {
  Entity Participant {
   attributes {
    Attribute name {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     isprimary
    }
    
    Attribute affiliation {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute nationality {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute address {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "Conference.editor"
    }
   }
  }
  Entity Talk {
   attributes {
    Attribute title {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     isprimary
    }
    Attribute ^description {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute speaker {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
   references {
    Reference {
     attribute"speaker"
     *Entity "Participant"
     attribute referenced "Participant.name"
     lower 1
     upper 1
    }
   }
  }
  Entity Room {
   attributes {
    Attribute roomname {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     isprimary
    }
    Attribute position {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
  }
  Entity AgendaItem {
   attributes {
    Attribute title {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     isprimary
    }
    Attribute time {
     dbtype = "datetime NOT NULL DEFAULT \'0000-00-00 00:00:00\'"
     htmltype = "date"
    }
    Attribute talk {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute room {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
   references {
    Reference {
     attribute"talk"
     *Entity "Talk"
     attribute referenced "Talk.title"
     lower 0
     upper 1
    }
    Reference {
     attribute"room"
     *Entity "Room"
     attribute referenced "Room.roomname"
     lower 1
     upper 1
    }
   }
  }
 }

 pages {
  IndexPage Participants {
   *Entities "Participant"
   filters Participant.name, 
    Participant.affiliation
  }
  DetailsPage Participant {
   *Entities "Participant" 
  }
  IndexPage Talks {
   *Entities "Talk", "Participant"
   filters Talk.title, 
    Talk.speaker
  }
  DetailsPage Talk {
   *Entities "Talk"
  }
  IndexPage Rooms {
   *Entities "Room"
   filters Room.roomname,
    Room.position
  }
  DetailsPage Room {
   *Entities "Room"
  }
  IndexPage Agendaitems {
      *Entities "AgendaItem"
      filters AgendaItem.room, 
       AgendaItem.talk, 
       AgendaItem.time, 
       AgendaItem.title
    }
  DetailsPage Agendaitem {
   *Entities "AgendaItem"
  }
 }

 extensions {
  Component JandBeyond {
   Manifestation {
    authors {
     Author "Dennis Priefer" {
      authoremail = "dennis.priefer@mni.thm.de"
      authorurl = "www.priefer.net"
     }
    }
    copyright = "Copyright (C) 20053 - 2014 Open Source Matters, Inc. All right reserved."
    license = "GNU General x Public License version 2 or later; see LICENSE.txt"
   }
   languages {
    Language de-DE {
       }
    Language en-GB {
    }
   }
   sections {
    Frontend section {
     *Pages Participants, Talks
    }
    Backend section {
     *Pages Participants, Participant, Talks, Talk, Rooms, Room, Agendaitems, Agendaitem
    }
   }
  }
 }
}