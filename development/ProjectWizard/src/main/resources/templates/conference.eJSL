eJSLModel "Conference" {
	eJSL part: CMS Extension {
		entities {
			Entity Participant {
				attributes {
					Attribute name {
						type = Short_Text
						Unique attribute with ID
					}
					Attribute affiliation {
						type = Text
					}
					Attribute nationality {
						type = Text
					}
					Attribute address {
						type = Text
					}
				}
			}
			Entity Talk {
				attributes {
					Attribute title {
						type = Short_Text
						Unique attribute
					}
					Attribute ^description {
						type = Text
					}
					Attribute speaker {
						type = Short_Text
					}
				}
				references {
					Reference {
						EntityAttribute = speaker
						*Entity = Participant
						*EntityReference = Participant.name
						lower = 1
						upper = 1
					}
				}
			}
			Entity Room {
				attributes {
					Attribute roomname {
						type = Short_Text
						Unique attribute
					}
					Attribute ^position {
						type = Text
					}
				}
			}
			Entity AgendaItem {
				attributes {
					Attribute title {
						type = Short_Text
						Unique attribute
					}
					Attribute time {
						type = Datetime
					}
					Attribute talk {
						type = Short_Text
					}
					Attribute room {
						type = Short_Text
					}
				}
				references {
					Reference {
						EntityAttribute = talk
						*Entity = Talk
						*EntityReference = Talk.title
						lower = 0
						upper = 1
					}
					Reference {
						EntityAttribute = room
						*Entity = Room
						*EntityReference = Room.roomname
						lower = 1
						upper = 1
					}
				}
			}
		}
		pages {
			IndexPage Participants {
				*Entities Participant
				table columns = Participant.name, Participant.address, Participant.affiliation
				filters = Participant.name, Participant.affiliation links {
					InternalcontextLink Details {
						target = Participant linked attribute = Participant.name
						linkparameters {
							Parameter name = *Attribute  "Participant.name"
						}
					}
				}
			}
			DetailsPage Participant {
				*Entities Participant links {
					InternalLink Index {
						target = Participants linked attribute = name
					}
				}
			}
			IndexPage Talks {
				*Entities Talk
				table columns = Talk.title, Talk.speaker
				filters = Talk.title, Talk.speaker links {
					InternalcontextLink Details {
						target = Talk linked attribute = Talk.title
						linkparameters {
							Parameter title = *Attribute  "Talk.title"
						}
					}
				}
			}
			DetailsPage Talk {
				*Entities Talk links {
					InternalLink Index {
						target = Talks linked attribute = title
					}
				}
			}
			IndexPage Rooms {
				*Entities Room
				table columns = Room.roomname, Room.^position
				filters = Room.roomname, Room.^position links {
					InternalcontextLink Details {
						target = Room linked attribute = Room.roomname
						linkparameters {
							Parameter roomname = *Attribute  "Room.roomname"
						}
					}
				}
			}
			DetailsPage Room {
				*Entities Room links {
					InternalLink Index {
						target = Rooms linked attribute = roomname
					}
				}
			}
			IndexPage Agendaitems {
				*Entities AgendaItem
				table columns = AgendaItem.room, AgendaItem.talk, AgendaItem.time, AgendaItem.title
				filters = AgendaItem.room, AgendaItem.talk, AgendaItem.time, AgendaItem.title links {
					InternalcontextLink Details {
						target = Agendaitem linked attribute = AgendaItem.title
						linkparameters {
							Parameter title = *Attribute  "AgendaItem.title"
						}
					}
				}
			}
			DetailsPage Agendaitem {
				*Entities AgendaItem links {
					InternalLink Index {
						target = Agendaitems linked attribute = AgendaItem.title
					}
				}
			}
		}
		extensions {
			Component Simple_Conference {
				Manifestation {
					authors {
						Author "John Doe" {
							authoremail = "john.doe@example.org"
						}
					}
					copyright = "Copyright (C) 2005 - 2016 Open Source Matters, Inc. All right reserved."
					license = "GNU General Public License version 2 or later; see LICENSE.txt"
					version = "1.0.1"
				}
				languages {
					Language de-DE {
					}
					Language en-GB {
					}
				}
				sections {
					Frontend section {
						*Pages {
							*Page : Participants
							*Page : Talks
						}
					}
					Backend section {
						*Pages {
							*Page : Participants
							*Page : Participant
							*Page : Talks
							*Page : Talk
							*Page : Rooms
							*Page : Room
							*Page : Agendaitems
							*Page : Agendaitem
						}
					}
				}
			}
		}
	}
}