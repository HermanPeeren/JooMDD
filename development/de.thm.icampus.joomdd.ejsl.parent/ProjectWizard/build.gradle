buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}


plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'eclipse'


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
		
    }
}


intellij{
    //version '143.116.4'			// old IntelliJ
    version  'IC-2016.3' 			// new IntelliJ distribution
	//alternativeIdePath "C:\\Program Files\\JetBrains\\PhpStorm 2017.2.4" //Local PhpStorm IDE
	alternativeIdePath "C:\\Program Files\\JetBrains\\PhpStorm 2017.1.4" //Local PhpStorm IDE
    updateSinceUntilBuild false		// assumption of old plugin.xml build
}

def xmlFile = file ('../pom.xml')
def xmlText = xmlFile.getText()
def parser = new XmlParser()
def list = parser.parseText(xmlText)
def version =  list.version[0].text() //version of Project

def buildPath = "build/"
def pluginSandbox = "build/idea-sandbox/plugins/"
def ideabuildpath = "../de.thm.icampus.joomdd.ejsl.idea/build/distributions/de.thm.icampus.joomdd.ejsl.idea-"+version+".zip"
def xtextIntelliJSource = "../../../releases/IntelliJ/org.eclipse.xtext.idea-2.10.0.zip"
def xtextPhpStormSource = "../../../releases/PhpStorm/xtext_plugin/PhpStorm16/Xtext.idea-2.10.0WithDependencies2016.1.2-.zip"
def phpStormeJSLPlugin = "../../../releases/PhpStorm/ideaRepository/de.thm.icampus.joomdd.ejsl.phpstorm-"+version+".zip"
def intelliJeJSLPlugin = "../../../releases/IntelliJ/ideaRepository/de.thm.icampus.joomdd.ejsl.idea-"+version+".zip"
def templatePath = "../de.thm.icampus.joomdd.ejsl.ui/templates/"

task cleanXtextPlugin(type: Delete){
    description 'delete old Xtext Plugin from Sandbox'
    group ('JetBrains_Wizard_Help_Task')
    delete pluginSandbox + "org.eclipse.xtext.idea"
}

task cleaneJSLPlugin(type: Delete){
    description 'delete old eJSL Plugin from Sandbox'
    group ('JetBrains_Wizard_Help_Task')
    delete pluginSandbox + "de.thm.icampus.joomdd.ejsl.idea"
}

task deleteOldWizardPlugin(type: Delete){
	description 'delete old Wizard Plugin from wizard Sources'
	group ('JetBrains_Wizard_Help_Task')
	delete buildPath+'wizardSources'
}

task addXtextPlugin(type: Copy, dependsOn: cleanXtextPlugin){
    description 'Add Xtext IDEA Core Plugin to Sandbox (IntelliJ or PhpStorm)'
    group ('JetBrains_Wizard_Help_Task')

    if (intellij.alternativeIdePath == null){
        // Load IntelliJ Xtet Plugin
        from zipTree(xtextIntelliJSource)
    }else{
        // Load PhpStorm Xtext Plugin
        from zipTree(xtextPhpStormSource)
    }
    into pluginSandbox
}

task addeJSLPlugin(type: Copy, dependsOn: cleaneJSLPlugin){
    description 'add latest eJSL Plugin to Sandbox'
    group ('JetBrains_Wizard_Help_Task')

    if (intellij.alternativeIdePath == null){
        // Load IntelliJ Xtet Plugin
        from zipTree(ideabuildpath)
    }else{
        // Load PhpStorm Xtext Plugin
        from zipTree(ideabuildpath)
    }
    into pluginSandbox
}

task buildIntelliJPlugin(dependsOn:'buildPlugin'){
    description 'Create standalone IntelliJ Wizard zip file'
    group ('JetBrains_Wizard_Help_Task')
}


task deleteTemplates(type: Delete){
	description 'delete old Templates'
	group ('JetBrains_Wizard_Help_Task')
	
	delete "src/main/resources/templates/"
	
}

task copyNewTemplates(type: Copy, dependsOn:'deleteTemplates'){
	description 'copy Templates from Ui Project'
	group ('JetBrains_Wizard_Help_Task')
	
	from templatePath
	into "src/main/resources/templates/"	
}

task createIntelliJWizardPluginSource(type: Copy, dependsOn:['copyNewTemplates','buildPlugin','deleteOldWizardPlugin']){
	description 'Creates IDEA Wizard Plugin source for Deployment'
	group ('JetBrains_Wizard_Help_Task')
	
	compileJava.mustRunAfter	copyNewTemplates

    from buildPath + 'resources/main'
    from buildPath + 'classes/main'
	into buildPath + 'wizardSources'
}

task JetBrainsIDEWithWizard(dependsOn:['copyNewTemplates','cleanXtextPlugin','cleaneJSLPlugin','runIdea']){
    description 'Create Wizard and run JetBrains IDE'
    group ('joomdd_Live')
	compileJava.mustRunAfter	copyNewTemplates
	
	doLast{
		println "Configure the IDE in Project Wizard/build.gradle file. Line 30 - 32: intellij{version ... for IntelliJ and alternativeIdePath ... for PhpStorm."
	}
}

task JetBrainsIDEWithAllPlugins(dependsOn:['copyNewTemplates','addXtextPlugin','addeJSLPlugin','runIdea']){
    description 'Create Wizard, add eJSL, Xtext and run IntelliJ'
    group ('JetBrains_Wizard_Help_Task')
    compileJava.mustRunAfter	copyNewTemplates
}
