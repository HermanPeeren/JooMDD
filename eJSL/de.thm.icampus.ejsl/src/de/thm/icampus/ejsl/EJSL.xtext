grammar de.thm.icampus.ejsl.EJSL with org.eclipse.xtext.common.Terminals
//import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
generate eJSL "http://www.thm.de/icampus/ejsl/EJSL"

EJSLModel returns EJSLModel:
	{EJSLModel}
	'eJSLModel'	name=STRING
	'{'
		('datatypes' '{' datatypes+=Datatype ( "," datatypes+=Datatype)* '}')?
		('globalparameters' '{' (globalparameters+=Parameter)* '}')?
		('parametergroups' '{' (parametergroups+=ParameterGroup)* '}')?
		('entities' '{' (entities+=Entity)* '}')?
		('datapackages' '{' (datapackages+=Datapackage)* '}')?
		('pages' '{' (pages+=Page)* '}')?
		('extensions' '{' (extensions+=Extension)* '}')?
	'}';

// data model
Type:
	DatatypeReference | SimpleDatatypes
;

DatatypeReference:
	type=[Datatype|STRING]
;

SimpleDatatypes:
	type=SimpleDatatypeKinds
;

Datatype returns Datatype:
	{Datatype}
	'Datatype' name=STRING;
	
Parameter returns Parameter:
	{Parameter}
	'Parameter' name=ID 
	'{'
		//'type' '=' ((dtype=[Datatype|QualifiedName]) | ('jvmtype' jvmtype=[jvmTypes::JvmType|QualifiedName]))
		'type' '=' ((dtype=Type))
		('defaultvalue' '=' defaultvalue=STRING)?
		('label' '=' label=STRING)?
		('size' '=' size=INT)?
		('description' '=' descripton=STRING)?
	'}'
;

ParameterGroup returns ParameterGroup:
	{ParameterGroup}
	'ParameterGroup' name=ID 
	'{'
		('label' '=' label=STRING)?
		('Parameters' '{' ((globalparameters+=[Parameter]) | (parameters+=Parameter))* '}')
	'}'
;

Datapackage returns Datapackage:
	{Datapackage}
	'Datapackage' name=ID
	'{'
		('datapackages' '{' (datapackages+=Datapackage)* '}')?
		('entities' '{' (entities+=Entity)* '}')?
		('datatypes' '{' (datatypes+=Datatype)* '}')?
	'}'
;

Entity returns Entity:
	{Entity}
	'Entity' name=MYID ('extends' supertype=[Entity|QualifiedName])?
	'{'
		('attributes' '{' (attributes+=Attribute)* '}')?
		// Kardinalit√§t
		('references' '{' (references+=Reference)* '}')?
	'}'
;

Attribute returns Attribute:
	{Attribute}
	//'Attribute' name=ID ':' ((dtype=[Datatype|QualifiedName]) | ('jvmtype' jvmtype=[jvmTypes::JvmType|QualifiedName]))
	'Attribute' name=MYID '{' 
		'dbtype' '=' (dbtype=Type)
		'htmltype' '=' (htmltype=Type)
		(isprimary ?= "Primary attribute")?
	'}'
;

Reference returns Reference:
	{Reference}
	'Reference'  
	'{'
		'Attribute' '=' attribute=[Attribute|QualifiedName]
		'*Entity' '=' entity=[Entity|QualifiedName]
		'Reference' '=' attributerefereced=[Attribute|QualifiedName]
		'lower' '=' lower=NUMBER
		'upper' '=' upper=NUMBER 
	'}'
;

// page model

Page:
	StaticPage | DynamicPage
;

StaticPage returns StaticPage:
	{StaticPage}
	'StaticPage' name=MYID 
	'{'
		('*ParameterGroups' parametergroups+=[ParameterGroup] (',' parametergroups+=[ParameterGroup])*)?
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('links' '{' (links+=Link)* '}')?
		'HTMLBody' '{' HTMLBody = STRING '}'
	'}'
;

DynamicPage:
	IndexPage | DetailsPage
;

IndexPage returns IndexPage:
	{IndexPage}
	'IndexPage' name=MYID 
	'{'
		('*Entities' entities+=[Entity|QualifiedName] (',' entities+=[Entity|QualifiedName])*)?
		('*ParameterGroups' parametergroups+=[ParameterGroup] (',' parametergroups+=[ParameterGroup])*)?
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('table columns' '=' tablecolumns+=[Attribute|QualifiedName] (',' tablecolumns+=[Attribute|QualifiedName])*)?
		('filters' '=' filters+=[Attribute|QualifiedName] (',' filters+=[Attribute|QualifiedName])*)?
		('links' '{' (links+=Link)* '}')?
	'}'
;

DetailsPage returns DetailsPage:
	{DetailsPage}
	'DetailsPage' name=MYID 
	'{'
		('*Entities' entities+=[Entity|QualifiedName] (',' entities+=[Entity|QualifiedName])*)?
		('*ParameterGroups' parametergroups+=[ParameterGroup] (',' parametergroups+=[ParameterGroup])*)?
		('*Globalparameters' globalparameters+=[Parameter] (',' globalparameters+=[Parameter])*)?
		('localparameters' '{' (localparameters+=Parameter)* '}')?
		('table columns' '=' tablecolumns+=[Attribute|QualifiedName] (',' tablecolumns+=[Attribute|QualifiedName])*)?
		('filters' '=' filters+=[Attribute|QualifiedName] (',' filters+=[Attribute|QualifiedName])*)?
		('links' '{' (links+=Link)* '}')?
	'}'
;

Link:
	ExternalLink | InternalLink 
;

ExternalLink returns ExternalLink:
	{ExternalLink}
	'ExternalLink' 
	'{'
		'target' '=' target=STRING
		'linked attribute' '=' linkedAttribute=[Attribute|QualifiedName]
		('label' '=' label=STRING)?
	'}'
;

InternalLink returns InternalLink:
	{InternalLink}
	('InternalLink' name=MYID 
	'{'
		'target' '=' target=[Page|QualifiedName]
		'linked attribute' '=' linkedAttribute=[Attribute|QualifiedName]
	'}') | ContextLink
;

ContextLink returns ContextLink:
	{ContextLink}
	'InternalcontextLink' name=MYID 
	'{'
		'target' '=' target=[Page|QualifiedName]
		'linked attribute' '=' linkedAttribute=[Attribute|QualifiedName]
		'linkparameters' '{' (linkparameters+=LinkParameter)* '}'
	'}'
;

LinkParameter returns LinkParameter:
	{LinkParameter}
	'Parameter' name=MYID '=' ('*Attribute 'attvalue=[Attribute|STRING] | value=STRING)
;

// extension model

Extension:
	ExtensionPackage | Component | Module | Plugin | Library | Template	
;

ExtensionPackage returns ExtensionPackage:
	{ExtensionPackage}
	'Extension package' name=MYID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		'extensions' '{' (extensions+=Extension)+ '}'
	'}'
;

Component returns Component:
	{Component}
	'Component' name=MYID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('Global parameter' '{' (globalParamter+=ParameterGroup)* '}')?
		('languages' '{' (languages+=Language)* '}')?
		'sections' '{' (sections+=Section)+ '}'
	'}'
;

Section:
	Backend | Frontend
;

Backend returns BackendSection:
	{BackendSection}
	'Backend section' 
	'{'
		'*Pages' '{'
			pageRef += PageReference*
		'}'
	'}'
;

PageReference returns PageReference:
	'*Page' ':' page=[Page|QualifiedName] ('from' ':' pagescr=[Component|QualifiedName] sect = SectionReference)?
;

Frontend returns FrontendSection:
	{FrontendSection}
	'Frontend section' 
	'{'
		'*Pages' '{'
			pageRef += PageReference*
		'}'
	'}'
;

Module returns Module:
	{Module}
	'Module' name=MYID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		(pageRef=PageReference)?
	'}'	
;

Plugin returns Plugin:
	{Plugin}
	'Plugin' name=MYID 
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		'Plugintype' '=' type=PluginKinds
		('languages' '{' (languages+=Language)* '}')?
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
		('parameters' '{' (localparameters+=Parameter)* '}')?
	'}'	
;

Library returns Library:
	{Library}
	'Library' name=MYID
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
		('classes' '{' (classes+=Class)* '}')?
		('packages' '{' (packages+=Package)* '}')?
	'}'
;

Package returns Package:
	{Package}
	'Package' name=MYID
	'{'
		('packages' '{' (packages+=Package)* '}')?
		('classes' '{' (classes+=Class)* '}')?
	'}'
;

Class returns Class:
	{Class}
    'Class' name=MYID ('extends' supertype=[Class|QualifiedName])?
    '{'
    	('*Class references' references+=[Class|QualifiedName] (',' references+=[Class|QualifiedName])*)?
    	('*Entities' entities+=[Entity|STRING] (',' entities+=[Entity|STRING])*)?
        ('methods' '{' (methods+=Method)* '}')?
    '}'
;

Method returns Method:
	{Method}
    'Method' name=MYID
    '{'
        //'Returnvalue' returnvalue=ID ':' type=[jvmTypes::JvmType|QualifiedName]
        ('Returnvalue' returnvalue=ID ':' type=Type)?
        ('methodparameters' '{' (methodparameters+=MethodParameter)* '}')?
    '}'
;

MethodParameter returns MethodParameter:
	{MethodParameter}
    //'MethodParameter' name=ID ':' type=[jvmTypes::JvmType|QualifiedName]
    'MethodParameter' name=ID ':' type=Type
;

Template returns Template:
	{Template}
	'Template' name=MYID
	'{'
		'Manifestation' '{' (manifest=Manifestation) '}'
		('languages' '{' (languages+=Language)* '}')?
		('parameters' '{' (localparameters+=Parameter)* '}')?
		('positions' '{' (positions+=Position)* '}')?
		('cssblocks' '{' (cssblocks+=CssBlock)* '}')?
	'}'
;

Manifestation returns Manifestation:
	{Manifestation}
	'authors' '{' (authors+=Author)+ '}'
	('creationdate' '=' creationdate=DATE)?
	('copyright' '=' copyright=STRING)?
	('license' '=' license=STRING)?
	('link' '=' link=STRING)?
	('version' '=' version=STRING)?
	('description' '=' description=STRING)?;

terminal DATE:
  ((('0'..'2') ('0'..'9') '.' ('02')) |
  (('0'..'2') ('0'..'9') '.' ((('0') (('1') | ('3'..'9'))) | ('1') ('0'..'2')) | (('1') ('0'..'2'))) |
  (('30') '.' ((('0') (('1') | ('3'..'9'))) | ('1') ('0'..'2'))) |
  (('31') '.' ((('0') (('1') | ('3') | ('5') | ('7') | ('8'))) | (('1') (('0')| ('2'))))))
  '.' ('0'..'2') ('0'..'9') ('0'..'9') ('0'..'9')
;

Author returns Author:
	{Author}
	'Author' name=STRING 
	'{'
		('authoremail' '=' authoremail=STRING)?
		('authorurl' '=' authorurl=STRING)?
	'}'
;

Language returns Language:
	{Language}
	'Language' name=LANGUAGE_CODE 
	'{'
    	('keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
    '}'
;

terminal LANGUAGE_CODE:
    'a'..'z' 'a'..'z' '-' 'A'..'Z' 'A'..'Z'
;

KeyValuePair returns KeyValuePair:
	{KeyValuePair}
	'Key' name=ID '=' value=STRING
;

Position returns Position:
	{Position}
	'Templateposition' (name=POSITION_TYPES | name=ID)
	'{'
		('positionparameters' '{' (positionparameters+=PositionParameter)* '}')?
	'}'
;

terminal POSITION_TYPES:
	'head' | 'contents' | 'footer' | 'left' | 'right'
;

PositionParameter:
	'Position Parameter' name = ID
	'{'
		('divId' '=' divid = ID)?
		('Positiontype' '=' type = POSITION_TYPES_NAMES)?
		('CSS keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
	'}'
;

terminal POSITION_TYPES_NAMES:
	'modules' | 'component'
;

CssBlock:
	'CssBlock' selector=STRING
	'('
		('keyvaluepairs' '{' (keyvaluepairs+=KeyValuePair)* '}')?
	')'
;

NUMBER : ('-')?INT;
	
QualifiedName: MYID ('.' MYID)*;

MYID : ('<')? ID ('>')?;

enum PluginKinds:
    authenticate | captcha | content | contact |
    editors | extensions | finder | quick_icons |
    search | system | user | xml_rpc
;

enum SimpleDatatypeKinds:
	string | bool | int | float | array
;
enum SectionReference:
	backend='.backend' | frontend='.frontend'
;