eJSLModel "Conference" {
 datatypes {
  Datatype "varchar(255) NOT NULL",
  Datatype "text NOT NULL",
  Datatype "datetime NOT NULL DEFAULT '0000-00-00 00:00:00'",
  Datatype "date",
  Datatype "textarea",
  Datatype "text",
  Datatype "editor"  
 }
 entities {
  Entity Participant {
   attributes {
    Attribute name {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     Primary attribute
    }
    
    Attribute affiliation {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute nationality {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute address {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "Conference.editor"
    }
   }
  }
  Entity Talk {
   attributes {
    Attribute title {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     Primary attribute 
    }
    Attribute ^description {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute speaker {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
   references {
    Reference {
    	Attribute = speaker 
    	*Entity = Participant
     	Reference = Participant.name
     lower =  1
     upper = 1
    }
   }
  }
  Entity Room {
   attributes {
    Attribute roomname {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     Primary attribute
    }
    Attribute position {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
  }
  Entity AgendaItem {
   attributes {
    Attribute title {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
     Primary attribute
    }
    Attribute time {
     dbtype = "datetime NOT NULL DEFAULT \'0000-00-00 00:00:00\'"
     htmltype = "date"
    }
    Attribute talk {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
    Attribute room {
     dbtype = "varchar(255) NOT NULL"
     htmltype = "text"
    }
   }
   references {
    Reference {
     Attribute = talk
     *Entity= Talk
     Reference = Talk.title
     lower=0
     upper = 1
    }
    Reference {
     Attribute = room
     *Entity = Room
      Reference = Room.roomname
     lower = 1
     upper = 1
    }
   }
  }
 }

 pages {
  IndexPage Participants {
   *Entities Participant
   table columns = Participant.name , Participant.address , Participant.affiliation
   filters = Participant.name, 
    Participant.affiliation
    links {
    	InternalcontextLink Details {
    		target= * Participant linked attribute =* Participant.name
    		linkparameters {
    			Parameter name = *Attribute  "Participant.name"
    		}
    	}
    }
  }
  DetailsPage Participant {
   *Entities Participant
   links {
   	 InternalLink Index {
   	 	target = * Participants linked attribute = * name
   	 }
   }
  }
  IndexPage Talks {
   *Entities Talk
     table columns = Talk.title , Talk.speaker
   filters = Talk.title, 
    Talk.speaker
     links {
    	InternalcontextLink Details {
    		target= * Talk linked attribute =* Talk.title
    		linkparameters {
    			Parameter title = *Attribute  "Talk.title"
    		}
    	}
    }
  }
  DetailsPage Talk {
   *Entities Talk
    links {
   	 InternalLink Index {
   	 	target = * Talks linked attribute = * title
   	 }
   }
  }
  IndexPage Rooms {
   *Entities Room
   table columns = Room.roomname,
    Room.position
   filters = Room.roomname,
    Room.position
    links {
    	InternalcontextLink Details {
    		target= * Room linked attribute =* Room.roomname
    		linkparameters {
    			Parameter roomname = *Attribute  "Room.roomname"
    		}
    	}
    }
  }
  DetailsPage Room {
   *Entities Room
   links {
   	 InternalLink Index {
   	 	target = * Rooms linked attribute = * roomname
   	 }
   }
  }
  IndexPage Agendaitems {
      *Entities AgendaItem
      table columns = AgendaItem.room, 
       AgendaItem.talk, 
       AgendaItem.time, 
       AgendaItem.title
      filters = AgendaItem.room, 
       AgendaItem.talk, 
       AgendaItem.time, 
       AgendaItem.title
       links {
    	InternalcontextLink Details {
    		target= * Agendaitem linked attribute =* AgendaItem.title
    		linkparameters {
    			Parameter title = *Attribute  "AgendaItem.title"
    		}
    	}
    }
    }
  DetailsPage Agendaitem {
   *Entities AgendaItem
   links {
   	 InternalLink Index {
   	 	target = * Agendaitems linked attribute = * AgendaItem.title
   	 }
   }
  }
 }

 extensions {
  Component JoomlaDays {
   Manifestation {
    authors {
     Author "Dennis Priefer" {
      authoremail = "dennis.priefer@mni.thm.de"
      authorurl = "www.priefer.net"
     }
    }
    copyright = "Copyright (C) 20053 - 2014 Open Source Matters, Inc. All right reserved."
    license = "GNU General x Public License version 22 orz later; see LICENSE.txt"
   }
   languages {
    Language de-DE {
       }
    Language en-GB {
    }
   }
   sections {
    Frontend section {
    	*Pages {
    		*Page: Participants
    		*Page: Talks
    	}
    }
    Backend section {
     *Pages {
     	*Page: Participants 
     	*Page: Participant
     	*Page: Talks
     	*Page: Talk
     	*Page: Rooms
     	*Page: Room
     	*Page: Agendaitems
     	*Page: Agendaitem
     	
     }
    }
   }
  }
 }
}