plugins {
	id 'war'
}
dependencies {
	compile project(':de.thm.icampus.joomdd.ejsl')
	compile project(':de.thm.icampus.joomdd.ejsl.ide')
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase.web:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.web.servlet:${xtextVersion}"
	compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
	runtime group: 'org.webjars', name: 'requirejs', version: '2.3.5'
	compile "org.webjars:jquery:2.2.4"
	compile "org.webjars:ace:1.2.8"
	compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.5.0'
	compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
	providedCompile "org.eclipse.jetty:jetty-annotations:9.2.11.v20150529"
	providedCompile "org.slf4j:slf4j-simple:1.7.12"
}
task jettyRun(type:JavaExec) {

	jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=9000'

	dependsOn(sourceSets.main.runtimeClasspath)
	classpath = sourceSets.main.runtimeClasspath.filter{it.exists()}
	main = 'de.thm.icampus.joomdd.ejsl.web.ServerLauncher'
	standardInput = System.in
	group = 'run'
	description = 'Starts an example Jetty server with your language'
}

task deleteXtendGen (type: Delete){
	description 'delete Xtend-Gen Folder for the Project'
	group ('JooMDD_Help_Task')
	
	delete('./xtend-gen/')
}

ant.importBuild 'CreateGenFolders.xml'

task runAntScript(dependsOn: make){
	description 'runs Ant Skript'
	group ('JooMDD_Help_Task')
}

task regenerateXtendGenFolder(dependsOn:['deleteXtendGen','runAntScript']){
	description 'delete and create xtend gen'
	group ('JooMDD')

	runAntScript.mustRunAfter	deleteXtendGen
}
